configure(allprojects) { project ->
    group = "cn.icodeit.cartman"
    version = qualifyVersionIfNecessary(version)

    ext.junitVersion         = "4.11"
    ext.nettyVersion         = "4.0.23.Final"
    ext.fastjsonVersion      = "1.2.2"

    ext.gradleScriptDir = "${rootProject.projectDir}/gradle"

    apply plugin: "java"
    apply plugin: "idea"

    compileJava {
        sourceCompatibility=1.8
        targetCompatibility=1.8
    }

    compileTestJava {
        sourceCompatibility=1.8
        targetCompatibility=1.8
    }

    sourceSets {
        main {
            java {
                srcDir "src/main/java"
            }
        }
    }

    test {
        systemProperty("java.awt.headless", "true")
        systemProperty("testGroups", project.properties.get("testGroups"))
        scanForTestClasses = false
        include(["**/*Tests.class", "**/*Test.class"])
        exclude(["**/Abstract*.class", '**/*$*'])
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile("junit:junit:${junitVersion}") {
            exclude group:'org.hamcrest', module:'hamcrest-core'
        }
        testCompile("org.hamcrest:hamcrest-all:1.3")
    }
}

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'
javadoc.options.charSet = 'UTF-8'

project("cartman-core") {
    description = "Cartman Core"

    dependencies {
        compile "io.netty:netty-all:${nettyVersion}"
        compile "com.alibaba:fastjson:${fastjsonVersion}"
    }
}

project("cartman-doc") {
    description = "Cartman Doc"

    dependencies {
        compile project(":cartman-core")
    }
}

def qualifyVersionIfNecessary(version) {
    if (rootProject.hasProperty("BRANCH_NAME")) {
        def qualifier = rootProject.getProperty("BRANCH_NAME")
        if (qualifier.startsWith("CTM-")) {
            return version.replace('BUILD', qualifier)
        }
    }
    return version
}